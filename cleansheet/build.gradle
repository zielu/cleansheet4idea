import java.nio.file.Paths
import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "org.jetbrains.intellij" version "1.12.0"
    id 'com.palantir.git-version' version '0.15.0'
    id 'ch.netzwerg.release' version '1.2.5'
}

repositories {
    mavenCentral()
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: "java"

def javaVersion = JavaVersion.VERSION_11

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

def fast = Boolean.valueOf(String.valueOf(findProperty('fast')))
def publish = Boolean.valueOf(String.valueOf(findProperty('publish')))

def vendor = 'LukaszZielinski'

configurations {
    testAgent {
        transitive = false
    }
}

intellij {
    version = ideaVersion
    type = ideaType
    updateSinceUntilBuild = false
    downloadSources = true
    sandboxDir = "${System.properties['user.home']}${File.separator}cleansheet4idea-build${File.separator}sandbox"
}

apply plugin: 'idea'
idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
}

compileJava.options.encoding = encoding
compileTestJava.options.encoding = encoding

patchPluginXml {
    changeNotes = "${file('change-notes.html').getText('UTF-8')}"
    pluginDescription = "${file('description.html').getText('UTF-8')}"
            .replace("RELEASE_DATETIME", buildTime())
            .replace("RELEASE_TYPE", buildType());
}

jar {
    manifest {
        attributes(
            'Name': project.name,
            'Specification-Title': project.name,
            'Specification-Vendor': vendor,
            'Specification-Version': project.version,
            'Implementation-Title': project.name,
            'Implementation-Vendor': vendor,
            'Implementation-Version': versionDetails().gitHash,
            'Build-Date': buildTime()
        )
    }
}

release {
    tagPrefix = ''
    dependsOn buildPlugin
}

if (publish) {
    apply from: 'publish.gradle'
    publishPlugin {
        token = project.publishToken
        username = project.publishUsername
        password = project.publishPassword
        channels = publishChannels()
    }
}

def static buildTime() {
    def df = new SimpleDateFormat("yyyyMMdd.HHmmss")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

def isStableBuild() {
    def details = versionDetails()
    return details.branchName ==~ /(master|\d+-release)/
}

def buildType() {
    def details = versionDetails()
    return details.branchName != "master" ? "EAP" : "Stable"
}

def publishChannels() {
    return isStableBuild() ? ['default', 'eap'] : ['eap']
}
